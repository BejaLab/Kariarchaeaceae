
from os import listdir, path
from pandas import read_excel
import yaml

rhodopsin_sets ,= glob_wildcards("rhodopsins/{rhodopsin_set}.faa")
metadata = read_excel("metadata/genomes.xlsx")

species = "Kariarchaeum pelagium"

non_red = metadata[metadata['redundant?'].isna()]
species_genomes = non_red[non_red['classification'].str.contains(species)]['genome'].tolist()
non_species_genomes = non_red[~non_red['classification'].str.contains(species)]['genome'].tolist()
all_genomes = species_genomes + non_species_genomes

configfile: "metadata/config.yaml"

rule all:
    input:
        # expand("analysis/genomes/{genome}_pfam_scan.jsonl", genome = genomes),
        expand("analysis/recomb/openrdp-amino-acid/{method}.txt", method = [ 'geneconv', 'bootscan', 'maxchi', 'siscan', 'chimaera', 'threeseq', 'rdp' ]),
        "analysis/recomb/gard-faa/GARD.json"

rule copy_genome:
    input:
        "genomes/{genome}.fna"
    output:
        "analysis/genomes/{genome}.fna"
    shell:
        "cp {input} {output}"

rule prodigal:
    input:
        "analysis/genomes/{genome}.fna"
    output:
        faa = "analysis/genomes/{genome}.faa",
        cds = "analysis/genomes/{genome}.cds",
        gff = "analysis/genomes/{genome}.gff"
    params:
        proc = lambda w: 'single' if w.genome in species_genomes else 'meta'
    shadow:
        "minimal"
    conda:
        "envs/prodigal.yaml"
    shell:
        "prodigal -i {input} -a {output.faa} -d {output.cds} -f gff -o {output.gff} -p {params.proc}"

rule prodigal_faa_files:
    input:
        "analysis/genomes/{genome}.faa"
    output:
        "analysis/homologs/input/{genome}.faa"
    conda:
        "envs/kits.yaml"
    shell:
        "seqkit replace -sp '[*]$' -o {output} {input}"

rule proteinortho:
    input:
        expand("analysis/homologs/input/{genome}.faa", genome = species_genomes)
    output:
        "analysis/homologs/proteinortho.proteinortho.tsv"
    shadow:
        "minimal"
    params:
        evalue = 1e-100,
        identity = 60
    threads:
        30
    conda:
        "envs/proteinortho.yaml"
    shell:
        "proteinortho -project=proteinortho -cpus={threads} -e={params.evalue} -identity={params.identity} {input} && mv proteinortho.* $(dirname {output})"

rule proteinortho_grab:
    input:
        fasta = expand("analysis/homologs/input/{genome}.faa", genome = species_genomes),
        tsv = "analysis/homologs/proteinortho.proteinortho.tsv"
    output:
        expand("analysis/homologs/output/{genome}.faa", genome = species_genomes)
    params:
        min = 4
    shadow:
        "minimal"
    conda:
        "envs/proteinortho.yaml"
    shell:
        """
        proteinortho_grab_proteins.pl -minprot={params.min} -core <(awk 'NR>1{{$1=NF-3}}1' OFS='\\t' {input.tsv}) {input.fasta} && \
            rename s/.core$// *.faa.core && mv *.faa $(dirname {output[0]})
        """

rule pfam_scan:
    input:
        "analysis/genomes/{genome}.faa"
    output:
        "analysis/genomes/{genome}_pfam_scan.jsonl"
    conda:
        "envs/pfam_scan.yaml"
    params:
        pfam = "pfam/hmmer/36.0"
    threads:
        2
    shell:
        "pfam_scan.pl -fasta {input} -dir {params.pfam} -json -cpu {threads} | jq -c .[] > {output}"

rule phylophlan_input:
    input:
        expand("analysis/homologs/output/{genome}.faa", genome = species_genomes)
    output:
        directory("analysis/proteins")
    shell:
        "mkdir {output} && cp {input} {output}/"

checkpoint phylophlan:
    input:
        work_dir = "analysis/proteins",
        cfg = "metadata/phylophlan.cfg",
        maas = "metadata/phylophlan.tsv"
    output:
        msa_dir = directory("analysis/proteins_phylophlan/data/trim_not_variant"),
        tree_dir= directory("analysis/proteins_phylophlan/data/gene_tree1"),
        trees = "analysis/proteins_phylophlan/proteins.tre"
    params:
        min_markers = 50,
        diversity = "low"
    conda:
        "envs/phylophlan.yaml"
    threads:
        workflow.cores
    shell:
        "phylophlan -i {input.work_dir} -t a -f {input.cfg} --diversity {params.diversity} --subsample full -d phylophlan --data_folder $(dirname {output.msa_dir}) --nproc {threads} --output_folder analysis --verbose --maas {input.maas} --min_num_markers {params.min_markers}"

def get_marker_files(file_name, dir_name = "analysis/proteins_cat_phylogeny/markers"):
    msa_dir = checkpoints.phylophlan.get().output['msa_dir']
    files = []
    for msa_file in listdir(msa_dir):
        marker, ext = path.splitext(msa_file)
        if ext == '.aln':
            file = f"{dir_name}/{marker}/{file_name}"
            files.append(file)
    return files

rule copy_msa:
    input:
        "analysis/proteins_phylophlan/data/trim_not_variant"
    output:
        "analysis/proteins_cat_phylogeny/markers/{marker}/input.fasta"
    shell:
        "cp {input}/{wildcards.marker}.aln {output}"

rule copy_tree:
    input:
        "analysis/proteins_phylophlan/data/gene_tree1"
    output:
        "analysis/proteins_cat_phylogeny/markers/{marker}/input.tree"
    params:
        prefix = "RAxML_result."
    shell:
        "cp {input}/{params.prefix}{wildcards.marker}.tre {output}"

rule treeshrink:
    input:
        msa = lambda w: get_marker_files("input.fasta"),
        tre = lambda w: get_marker_files("input.tree")
    output:
        touch("analysis/proteins_cat_phylogeny/treeshrink.ok")
    params:
        dir_name = "analysis/proteins_cat_phylogeny/markers",
        q = 0.05
    conda:
        "envs/treeshrink.yaml"
    shell:
        "run_treeshrink.py -i {params.dir_name} -O treeshrink -q {params.q} --force"

rule cat_msas:
    input:
        lambda w: get_marker_files("input.fasta"),
        "analysis/proteins_cat_phylogeny/treeshrink.ok"
    output:
        msa = "analysis/proteins_cat_phylogeny/treeshrink.fasta",
        part = "analysis/proteins_cat_phylogeny/treeshrink.part"
    params:
        fasta = lambda w: get_marker_files("treeshrink.fasta")
    conda:
        "envs/phyx.yaml"
    shell:
        "pxcat -s {params.fasta} -o {output.msa} -p {output.part}"

rule completeness:
    input:
        "analysis/homologs/proteinortho.proteinortho.tsv"
    output:
        "analysis/pangenome/completeness.tsv"
    params:
        genomes = species_genomes,
        fraction = 0.25
    conda:
        "envs/r.yaml"
    script:
        "scripts/completeness.R"

rule break_scaffolds:
    input:
        "genomes/{genome}.fna"
    output:
        "analysis/pangenome/contigs/{genome}.fna"
    params:
        gap = 10
    conda:
        "envs/biopython.yaml"
    script:
        "scripts/break_scaffolds.py"

rule copy_genome_for_pangenome:
    input:
        "genomes/{genome}.fna"
    output:
        "analysis/pangenome/genomes/{genome}.fna"
    shell:
        "cp {input} {output}"

rule superpang:
    input:
        genomes = expand("genomes/{genome}.fna", genome = species_genomes),
        completeness = "analysis/pangenome/completeness.tsv"
    output:
        expand("analysis/pangenome/superpang/{file}", file = [ "assembly.fasta", "assembly.info.tsv" ]),
        expand("analysis/pangenome/superpang/{file}", file = [ "graph.fastg", "graph.NBP2origins.csv" ]),
        expand("analysis/pangenome/superpang/{file}", file = [ "NBP2origins.tsv", "NBPs.accessory.fasta", "NBPs.core.fasta", "NBPs.fasta" ])
    log:
        "analysis/pangenome/superpang.log"
    params:
        ident = 0.8,
        k = 101,
        gap = 10
    conda:
        "envs/superpang.yaml"
    threads:
        20
    shell:
        "SuperPang.py -f {input.genomes} -o $(dirname {output[0]}) -t {threads} -q {input.completeness} -k {params.k} -i {params.ident} -b {params.ident} --gap-size {params.gap} --force-overwrite &> {log}"

rule raxml:
    input:
        "analysis/proteins_cat_phylogeny/treeshrink.fasta"
    output:
        "analysis/proteins_cat_phylogeny/RAxML_info.txt",
        "analysis/proteins_cat_phylogeny/RAxML_bipartitions.txt"
    params:
        model = "PROTCATLG",
        seed = 123,
        bootstrap = 1000
    conda:
        "envs/phylophlan.yaml"
    threads:
        20
    shell:
        "raxmlHPC-PTHREADS-SSE3 -f a -p {params.seed} -x {params.seed} -# {params.bootstrap} -m {params.model} -T {threads} -s {input} -n txt -w $(dirname $(realpath {output}))"

rule taxit:
    input:
        tree = "analysis/proteins_cat_phylogeny/RAxML_bipartitions.txt",
        info = "analysis/proteins_cat_phylogeny/RAxML_info.txt",
        aln = "analysis/proteins_cat_phylogeny/treeshrink.fasta"
    output:
        directory("analysis/proteins_cat_phylogeny/RAxML.refpkg")
    conda:
        "envs/pplacer.yaml"
    shell:
        "taxit create -l locus_tag -P {output} --tree-file {input.tree} --aln-fasta {input.aln} --tree-stats {input.info}"

rule pplacer:
    input:
        refpkg = "analysis/proteins_cat_phylogeny/RAxML.refpkg",
        fasta  = "analysis/proteins_cat_phylogeny/pplacer_input.fasta"
    output:
        "analysis/proteins_cat_phylogeny/pplacer.jplace"
    conda:
        "envs/pplacer.yaml"
    shell:
        "pplacer -o {output} -c {input.refpkg} {input.fasta}"

rule makeblast_prot:
    input:
        "{prefix}.faa"
    output:
        "{prefix}.faa.pdb"
    conda:
        "envs/blast.yaml"
    shell:
        "makeblastdb -in {input} -dbtype prot"

rule makeblast_nucl:
    input:
        "{prefix}.fna"
    output:
        "{prefix}.fna.ndb"
    conda:
        "envs/blast.yaml"
    shell:
        "makeblastdb -in {input} -dbtype nucl"

rule blastp:
    input:
        query = "metadata/georgia.faa",
        db = "analysis/genomes/{genome}.faa",
        pdb = "analysis/genomes/{genome}.faa.pdb"
    output:
        "analysis/georgia/blastp/{genome}.txt"
    conda:
        "envs/blast.yaml"
    shell:
        "blastp -query {input.query} -db {input.db} -outfmt 6 -out {output}"

rule blastp_collect:
    input:
        fasta = expand("analysis/genomes/{genome}.faa", genome = species_genomes),
        blast = expand("analysis/georgia/blastp/{genome}.txt", genome = species_genomes),
        query = "metadata/georgia.faa",
        msa = "analysis/proteins_cat_phylogeny/treeshrink.fasta"
    output:
        fasta = "analysis/proteins_cat_phylogeny/pplacer_input.fasta",
        part = "analysis/proteins_cat_phylogeny/pplacer_input.part"
    params:
        genomes = species_genomes,
        query_genome = "Kari_Gs0128817",
        evalue = 1e-15,
        min_genes = 3
    conda:
        "envs/biopython.yaml"
    script:
        "scripts/collect_homologs.py"

rule gappa:
    input:
        "analysis/proteins_cat_phylogeny/pplacer.jplace"
    output:
        "analysis/proteins_cat_phylogeny/pplacer.newick"
    conda:
        "envs/gappa.yaml"
    shell:
        "gappa examine graft --jplace-path {input} --out-dir $(dirname {output})"

rule rhodopsins_cat:
    input:
        expand("rhodopsins/{rhodopsin_set}.faa", rhodopsin_set = rhodopsin_sets)
    output:
        "analysis/rhodopsins/proteins.faa"
    conda:
        "envs/kits.yaml"
    shell:
        "seqkit grep -vsrp XXX -o {output} {input}"

rule residues:
    input:
        "analysis/rhodopsins/mafft.faa"
    output:
        "analysis/rhodopsins/residues.txt"
    params:
        residues = config['residues']
    conda:
        "envs/biopython.yaml"
    script:
        "scripts/residues.py"

rule rhodopsins_mafft:
    input:
        "analysis/rhodopsins/proteins.faa"
    output:
        "analysis/rhodopsins/mafft.faa"
    conda:
        "envs/phylophlan.yaml"
    shell:
        "cat {input} | mafft --auto --reorder - > {output}"

rule rhodopsins_trimal:
    input:
        "analysis/rhodopsins/mafft.faa"
    output:
        "analysis/rhodopsins/trimal.faa"
    params:
        gt = 0.9
    conda:
        "envs/phylophlan.yaml"
    shell:
        "trimal -gt {params.gt} -in {input} -out {output}"

rule rhodopsins_raxml:
    input:
        "analysis/rhodopsins/trimal.faa"
    output:
        "analysis/rhodopsins/RAxML_info.txt",
        "analysis/rhodopsins/RAxML_bipartitions.txt",
        "analysis/rhodopsins/RAxML_bestTree.txt"
    params:
        model = "PROTGAMMAAUTO",
        seed = 123,
        bootstrap = 1000
    conda:
        "envs/phylophlan.yaml"
    threads:
        20
    shell:
        "raxmlHPC-PTHREADS-SSE3 -f a -p {params.seed} -x {params.seed} -# {params.bootstrap} -m {params.model} -T {threads} -s {input} -n txt -w $(dirname $(realpath {output}))"
        # "raxmlHPC-PTHREADS-SSE3 -p {params.seed} -m {params.model} -T {threads} -s {input} -n txt -w $(dirname $(realpath {output}))"

rule rhodopsins_plot:
    input:
        # newick = "analysis/rhodopsins/RAxML_bipartitions.txt",
        metadata = "metadata/rhodopsins.xlsx"
    output:
        plot = "output/rhodopsins.svg",
        jtree = "output/rhodopsins.jtree"
    conda:
        "envs/r.yaml"
    script:
        "scripts/plot_protein_tree.R"

rule superpang_filter:
    input:
        info = "analysis/pangenome/superpang/assembly.info.tsv",
        fasta = "analysis/pangenome/superpang/assembly.fasta"
    output:
        "analysis/pangenome/pgap/assembly.fna"
    params:
        set = 'core'
    conda:
        "envs/kits.yaml"
    shell:
        "grep -E core {input.info} | cut -f1 | seqkit grep -f- {input.fasta} -o {output}"

rule copy_submol:
    input:
        "metadata/pgap.yaml"
    output:
        "analysis/pangenome/pgap/{genome}_submol.yaml"
    shell:
        "cp {input} {output}"

rule make_yaml:
    input:
        fasta  = "analysis/pangenome/pgap/{genome}.fna",
        submol = "analysis/pangenome/pgap/{genome}_submol.yaml"
    output:
        "analysis/pangenome/pgap/{genome}.yaml"
    wildcard_constraints:
        genome = '.*(?<!_submol)'
    run:
        sets = dict(
            fasta  = { "class": "File", "location": path.basename(input.fasta)  },
            submol = { "class": "File", "location": path.basename(input.submol) }
        )
        with open(output[0], 'w') as fd:
            yaml.dump(sets, fd)

rule pgap:
    input:
        yaml = "analysis/pangenome/pgap/{genome}.yaml",
        submol = "analysis/pangenome/pgap/{genome}_submol.yaml"
    output:
        gbk = "analysis/pangenome/pgap/{genome}/annot.gbk"
    params:
        dirname = directory("analysis/pangenome/pgap/{genome}/")
    threads:
        8
    shell:
        """
        rm -fr {params.dirname}
        pgap.py --no-internet --no-self-update --debug --ignore-all-errors --report-usage-true --cpu {threads} -o {params.dirname} {input.yaml}
        """

rule checkm_dload:
    output:
        directory("analysis/checkm_database")
    params:
        url = "https://data.ace.uq.edu.au/public/CheckM_databases/checkm_data_2015_01_16.tar.gz"
    shell:
        "mkdir -p {output} && wget -O- {params.url} | tar xvz -C {output}"

rule checkm_all:
    input:
        expand("analysis/checkm/{genome}", genome = species_genomes)

rule checkm_genomes:
    input:
        data = "analysis/checkm_database",
        fasta = "analysis/genomes/{genome}.fna"
    output:
        dir = directory("analysis/checkm/{genome}"),
        txt = "analysis/checkm/{genome}.txt"
    params:
        domain = "Archaea"
    conda:
        "envs/checkm.yaml"
    shadow:
        "minimal"
    threads:
        10
    shell:
        "CHECKM_DATA_PATH={input.data} checkm taxonomy_wf domain {params.domain} $(dirname {input.fasta}) {output.dir} -f {output.txt} -t {threads}"

rule checkm_pangenome:
    input:
        data = "analysis/checkm_database",
        fasta = "analysis/pangenome/pgap/assembly.fna"
    output:
        directory("analysis/pangenome/checkm")
    params:
        domain = "Archaea"
    conda:
        "envs/checkm.yaml"
    threads:
        20
    shell:
        "CHECKM_DATA_PATH={input.data} checkm taxonomy_wf domain {params.domain} $(dirname {input.fasta}) {output} -t {threads}"

# not available from conda
rule splitstree_run:
    input:
        fasta = "analysis/rhodopsins/trimal.faa",
        nexus = "metadata/splitstree.nex"
    output:
        "analysis/rhodopsins/splitstree.nex"
    shell:
        "xvfb-run -a SplitsTreeCMD -x 'IMPORT FILE={input.fasta} DATATYPE=PROTEIN; EXECUTE FILE={input.nexus}; UPDATE; SAVE FILE={output}; QUIT;'"

rule plot_rhodopsins:
    input:
        residues = "analysis/rhodopsins/residues.txt",
        # tree = "analysis/rhodopsins/RAxML_bipartitions.txt",
        network = "analysis/rhodopsins/splitstree.nex",
        metadata = "metadata/rhodopsins.xlsx"
    output:
        "output/rhodopsin_network.svg"
    conda:
        "envs/r.yaml"
    script:
        "scripts/plot_protein_network.R"

rule cluster_rhodopsins:
    input:
        "analysis/rhodopsins/proteins.faa"
    output:
        "analysis/rhodopsins/proteins.cdhit"
    params:
        c = 0.9
    conda:
        "envs/cd-hit.yaml"
    shell:
        "cd-hit -c {params.c} -d 0 -i {input} -o {output}"

rule tblastn_fna:
    input:
        query = "analysis/rhodopsins/proteins.cdhit",
        ndb = "databases/fna/{database}.ndb"
    output:
        "analysis/tblastn/{database}.txt"
    params:
        db = "databases/fna/{database}",
        evalue = 1e-5
    conda:
        "envs/blast.yaml"
    threads:
        40
    shell:
        "tblastn -query {input.query} -db {params.db} -outfmt 6 -out {output} -max_target_seqs 1000000000 -num_threads {threads} -evalue {params.evalue}"

rule blastp_database:
    input:
        query = "rhodopsins/{family}.faa",
        pdb = "databases/faa/{database}.pdb"
    output:
        "analysis/blastp/{database}-{family}.txt"
    params:
        db = "databases/faa/{database}",
        evalue = 1e-10,
        cols = [ 'qseqid', 'sseqid', 'pident', 'length', 'qlen', 'slen', 'qseq', 'sseq', 'qstart', 'qend', 'sstart', 'send', 'evalue', 'bitscore', 'stitle' ]
    conda:
        "envs/blast.yaml"
    threads:
        20
    shell:
        "blastp -query {input.query} -db {params.db} -outfmt '6 {params.cols}' -out {output} -max_target_seqs 1000000000 -num_threads {threads} -evalue {params.evalue}"

rule blastp_gtdb:
    input:
        query = "rhodopsins/{family}.faa",
        pdb = "analysis/rhodopsin_cds/GTDB/coding.faa.pdb"
    output:
        "analysis/blastp/GTDB-{family}.txt"
    params:
        db = "analysis/rhodopsin_cds/GTDB/coding.faa",
        evalue = 1e-10,
        cols = [ 'qseqid', 'sseqid', 'pident', 'length', 'qlen', 'slen', 'qseq', 'sseq', 'qstart', 'qend', 'sstart', 'send', 'evalue', 'bitscore', 'stitle' ]
    conda:
        "envs/blast.yaml"
    threads:
        20
    shell:
        "blastp -query {input.query} -db {params.db} -outfmt '6 {params.cols}' -out {output} -max_target_seqs 1000000000 -num_threads {threads} -evalue {params.evalue}"

rule cat_genomes:
    input:
        expand("analysis/genomes/{genome}.faa", genome = all_genomes)
    output:
        "analysis/genomes/all_genomes.faa"
    shell:
        "cat {input} > {output}"

rule blastp_genomes:
    input:
        query = "rhodopsins/{family}.faa",
        pdb = "analysis/genomes/all_genomes.faa.pdb"
    output:
        "analysis/blastp/all_genomes-{family}.txt"
    params:
        db = "analysis/genomes/all_genomes.faa",
        evalue = 1e-10,
        cols = [ 'qseqid', 'sseqid', 'pident', 'length', 'qlen', 'slen', 'qseq', 'sseq', 'qstart', 'qend', 'sstart', 'send', 'evalue', 'bitscore', 'stitle' ]
    conda:
        "envs/blast.yaml"
    threads:
        20
    shell:
        "blastp -query {input.query} -db {params.db} -outfmt '6 {params.cols}' -out {output} -max_target_seqs 1000000000 -num_threads {threads} -evalue {params.evalue}"

rule blastp_faa:
    input:
        "analysis/blastp/{database}_merged.csv"
    output:
        "analysis/blastp/{database}_merged.faa"
    conda:
        "envs/kits.yaml"
    shell:
        "csvcut -c sseqid,stitle,sseq {input} | csvformat -T | sed 1d | sed 's/\\t/ /' | seqkit tab2fx | seqkit seq -g -o {output}"

rule easy_taxonomy:
    input:
        db_tax = "analysis/rhodopsin_cds/GTDB/coding_taxonomy",
        db = "analysis/rhodopsin_cds/GTDB/coding",
        query = "analysis/blastp/{database}_merged.faa"
    output:
        "analysis/blastp/{database}_merged_lca.tsv"
    params:
        lca_mode = 4,
        prefix = "analysis/blastp/{database}_merged"
    conda:
        "envs/kits.yaml"
    threads:
        32
    shell:
        "mmseqs easy-taxonomy {input.query} {input.db} {params.prefix} tmp --lca-mode {params.lca_mode} --tax-lineage 1 --threads {threads}"

rule all_clades_csv:
    input:
        expand("analysis/blastp/{database}_clades.csv", database = [ 'JGI_IMG_unrestricted.faa', 'OM-RGC_v2_orfs.faa', 'all_genomes', 'GTDB' ])

rule subclades_poseidoniia:
    input:
        expand("analysis/blastp/{database}-{{clade}}.faa", database = [ 'JGI_IMG_unrestricted.faa', 'OM-RGC_v2_orfs.faa', 'GTDB' ])
    output:
        "analysis/subclades/poseidoniia-{clade}/sequences.fasta"
    params:
        m = 210
    conda:
        "envs/kits.yaml"
    shell:
        "seqkit seq -m {params.m} -o {output} {input}"

rule subclades_heidallR:
    input:
        expand("analysis/blastp/{database}-{clade}.faa", database = [ 'all_genomes', 'JGI_IMG_unrestricted.faa', 'OM-RGC_v2_orfs.faa' ], clade = [ 'HeimdallR' ])
    output:
        "analysis/subclades/kariarchaeum-heimdallR/sequences.fasta"
    params:
        m = 210
    conda:
        "envs/kits.yaml"
    shell:
        "seqkit seq -m {params.m} {input} | seqkit rmdup -s -o {output}"

rule subclades_poseidoniia_cdhit:
    input:
        "analysis/subclades/poseidoniia-{clade}/sequences.fasta"
    output:
        "analysis/subclades/poseidoniia-{clade}/cdhit.fasta"
    params:
        c = 0.9
    conda:
        "envs/cd-hit.yaml"
    shell:
        "cd-hit -i {input} -o {output} -c {params.c} -d 0"

rule subclades_kariarchaeum_cdhit:
    input:
        "analysis/subclades/kariarchaeum-heimdallR/sequences.fasta"
    output:
        "analysis/subclades/kariarchaeum-heimdallR/cdhit.fasta"
    params:
        c = 1.0
    conda:
        "envs/cd-hit.yaml"
    shell:
        "cd-hit -i {input} -o {output} -c {params.c} -d 0"

rule subclades_mafft:
    input:
        "analysis/subclades/{taxon}-{clade}/cdhit.fasta",
        "rhodopsins/references.faa"
    output:
        "analysis/subclades/{taxon}-{clade}/mafft.fasta"
    conda:
        "envs/phylophlan.yaml"
    shell:
        "cat {input} | mafft --reorder --auto - > {output}"

rule subclades_trimal:
    input:
        "analysis/subclades/{taxon}-{clade}/mafft.fasta"
    output:
        "analysis/subclades/{taxon}-{clade}/trimal.fasta"
    params:
        gt = 0.1
    conda:
        "envs/phylophlan.yaml"
    shell:
        "trimal -in {input} -out {output} -gt {params.gt} -keepheader"

rule subclades_trimal_long:
    input:
        "analysis/subclades/{taxon}-{clade}/trimal.fasta"
    output:
        "analysis/subclades/{taxon}-{clade}/trimal.long.fasta"
    params:
        m = 210
    conda:
        "envs/kits.yaml"
    shell:
        "seqkit seq -nigm {params.m} {input} | seqkit grep -o {output} -f- {input}"

rule subclades_raxml:
    input:
        "analysis/subclades/{taxon}-{clade}/trimal.long.fasta"
    output:
        "analysis/subclades/{taxon}-{clade}/RAxML_info.txt",
        "analysis/subclades/{taxon}-{clade}/RAxML_bipartitions.txt"
    params:
        model = "PROTCATLG",
        seed = 123,
        bootstrap = 1000
    conda:
        "envs/phylophlan.yaml"
    threads:
        20
    shell:
        "raxmlHPC-PTHREADS-SSE3 -f a -p {params.seed} -x {params.seed} -# {params.bootstrap} -m {params.model} -T {threads} -s {input} -n txt -w $(dirname $(realpath {output}))"

rule blastp_merge_hits:
    input:
        outfmt6 = expand("analysis/blastp/{{database}}-{family}.txt", family = rhodopsin_sets),
        metadata = "metadata/rhodopsins.xlsx"
    output:
        "analysis/blastp/{database}_merged.csv"
    params:
        cols = [ 'qseqid', 'sseqid', 'pident', 'length', 'qlen', 'slen', 'qseq', 'sseq', 'qstart', 'qend', 'sstart', 'send', 'evalue', 'bitscore', 'stitle' ],
        id = 50,
        score = 90,
        clades = [ 'HeimdallR', 'ACA', 'ACB' ]
    conda:
        "envs/pandas.yaml"
    script:
        "scripts/extract_hits.py"

rule blastp_filter_taxa:
    input:
        csv = "analysis/blastp/{database}_merged.csv",
        lca = "analysis/blastp/{database}_merged_lca.tsv"
    output:
        "analysis/blastp/{database}_clades.csv"
    params:
        taxon = "d_Archaea"
    conda:
        "envs/kits.yaml"
    shell:
        "grep {params.taxon} {input.lca} | cut -f1 | csvgrep -c sseqid -f- {input.csv} > {output}"

rule blastp_clade_faa:
    input:
        "analysis/blastp/{database}_clades.csv"
    output:
        "analysis/blastp/{database}-{family}.faa"
    params:
        clade = lambda w: { 'HeimdallR': 'HeimdallR', 'ACB': 'ACB', 'ACA': 'PR' }[w.family],
        subclade = lambda w: { 'HeimdallR': '', 'ACB': '', 'ACA': 'ACA' }[w.family]
    conda:
        "envs/kits.yaml"
    shell:
        "csvgrep -c clade -m '{params.clade}' {input} | csvgrep -c subclade -m '{params.subclade}' | csvcut -c sseqid,stitle,sseq | csvformat -T | sed 1d | sed 's/\\t/ /' | seqkit tab2fx | seqkit seq -g -o {output}"

rule dload_map:
    output:
        "analysis/maps/land/ne_110m_land.shp"
    params:
        url = "https://www.naturalearthdata.com/http//www.naturalearthdata.com/download/110m/physical/ne_110m_land.zip"
    shadow:
        "minimal"
    shell:
        "wget -O tmp.zip '{params.url}' && unzip tmp.zip -d $(dirname {output})"

rule om_rgc_profiles:
    input:
        clades = "analysis/blastp/OM-RGC_v2_orfs.faa_clades.csv",
        profile_metaG = "databases/faa/OM-RGC_v2_gene_profile_metaG.tsv",
        profile_metaT = "databases/faa/OM-RGC_v2_gene_profile_metaT.tsv",
        metadata = "databases/faa/Salazar_et_al_2019_Suppl_Info.xlsx"
    output:
        profiles = "analysis/profiles/OM-RGC_v2_orfs_profiles.csv",
        matches = "analysis/profiles/OM-RGC_v2_orfs_matches.csv",
        samples = "analysis/profiles/OM-RGC_v2_orfs_samples.csv"
    conda:
        "envs/pandas.yaml"
    script:
        "scripts/om_rgc_profiles.py"

rule jgi_img_profiles:
    input:
        clades = "analysis/blastp/JGI_IMG_unrestricted.faa_clades.csv",
        profile = "databases/faa/JGI_IMG_unrestricted.gene_info.txt"
    output:
        profiles = "analysis/profiles/JGI_IMG_unrestricted_profiles.csv",
        matches = "analysis/profiles/JGI_IMG_unrestricted_matches.csv",
        samples = "analysis/profiles/JGI_IMG_unrestricted_samples.csv"
    conda:
        "envs/pandas.yaml"
    script:
        "scripts/jgi_img_profiles.py"

rule plot_map:
    input:
        genomes = "metadata/genomes.xlsx",
        shape = "analysis/maps/land/ne_110m_land.shp",
        jgi_img_profiles = "analysis/profiles/JGI_IMG_unrestricted_profiles.csv",
        jgi_img_matches = "analysis/profiles/JGI_IMG_unrestricted_matches.csv",
        jgi_img_samples = "analysis/profiles/JGI_IMG_unrestricted_samples.csv",
        om_rgc_profiles = "analysis/profiles/OM-RGC_v2_orfs_profiles.csv",
        om_rgc_matches = "analysis/profiles/OM-RGC_v2_orfs_matches.csv",
        om_rgc_samples = "analysis/profiles/OM-RGC_v2_orfs_samples.csv"
    output:
        "output/map.pdf"
    conda:
        "envs/r-map.yaml"
    script:
        "scripts/plot_map.R"

rule tblastn_filter_hits:
    input:
        outfmt6 = "analysis/tblastn/{database}.txt",
        metadata = "metadata/rhodopsins.xlsx"
    output:
        "analysis/tblastn/{database}.csv"
    params:
        id = 40,
        length = 180,
        evalue = 1e-10
    conda:
        "envs/pandas.yaml"
    script:
        "scripts/extract_hits.py"

rule ref_genomes_cat:
    input:
        expand("genomes/{genome}.fna", genome = species_genomes),
        "metadata/georgia.fna"
    output:
        "analysis/ref_genomes/genomes.fna"
    shell:
        "cat {input} > {output}"

rule ref_genomes_makeblastdb:
    input:
        "analysis/ref_genomes/genomes.fna"
    output:
        "analysis/ref_genomes/genomes.fna.ndb"
    conda:
        "envs/blast.yaml"
    shell:
        "makeblastdb -in {input} -dbtype nucl"

rule ref_genomes_tblastn:
    input:
        query = "rhodopsins/HeimdallR.faa",
        ndb = "analysis/ref_genomes/genomes.fna.ndb",
        db = "analysis/ref_genomes/genomes.fna"
    output:
        "analysis/tblastn/ref_genomes.txt"
    params:
        cols = [ 'qseqid', 'sseqid', 'pident', 'length', 'qlen', 'slen', 'qstart', 'qend', 'sstart', 'send', 'evalue', 'bitscore', 'sseq', 'stitle' ],
        evalue = 1e-30
    conda:
        "envs/blast.yaml"
    shell:
        "tblastn -query {input.query} -db {input.db} -out {output} -outfmt '6 {params.cols}' -evalue {params.evalue}"

rule link_gtdb_coding:
    input:
        "databases/rhodopsins/GTDB_coding.fna"
    output:
        "analysis/rhodopsin_cds/GTDB/coding.fna"
    shell:
        "cp {input} {output}"

rule gtdb_coding_translate:
    input:
        "analysis/rhodopsin_cds/GTDB/coding.fna"
    output:
        "analysis/rhodopsin_cds/GTDB/coding.faa"
    conda:
        "envs/kits.yaml"
    shell:
        "seqkit seq -g {input} | seqkit translate -o {output}"

rule gtdb_coding_tax:
    input:
        "analysis/rhodopsin_cds/GTDB/coding.fna"
    output:
        nodes = "analysis/rhodopsin_cds/GTDB/nodes.dmp",
        names = "analysis/rhodopsin_cds/GTDB/names.dmp",
        mapping = "analysis/rhodopsin_cds/GTDB/mapping",
        merged = "analysis/rhodopsin_cds/GTDB/merged.dmp",
        delnodes = "analysis/rhodopsin_cds/GTDB/delnodes.dmp"
    conda:
        "envs/biopython.yaml"
    script:
        "scripts/gtdb_tax.py"

rule gtdb_codon_createdb:
    input:
        "analysis/rhodopsin_cds/GTDB/coding.faa"
    output:
        "analysis/rhodopsin_cds/GTDB/coding"
    conda:
        "envs/mmseqs2.yaml"
    shell:
        "mmseqs createdb {input} {output}"

rule gtdb_codon_createtaxdb:
    input:
        db = "analysis/rhodopsin_cds/GTDB/coding",
        nodes = "analysis/rhodopsin_cds/GTDB/nodes.dmp",
        names = "analysis/rhodopsin_cds/GTDB/names.dmp",
        mapping = "analysis/rhodopsin_cds/GTDB/mapping",
        merged = "analysis/rhodopsin_cds/GTDB/merged.dmp",
        delnodes = "analysis/rhodopsin_cds/GTDB/delnodes.dmp"
    output:
        "analysis/rhodopsin_cds/GTDB/coding_taxonomy"
    conda:
        "envs/mmseqs2.yaml"
    shell:
        "mmseqs createtaxdb {input.db} tmp --ncbi-tax-dump $(dirname {input.nodes}) --tax-mapping-file {input.mapping}"

rule tblastn_gtdb:
    input:
        query = "rhodopsins/{clade}.faa",
        ndb = "databases/gtdb/gtdb_genomes_reps_r214.fna.ndb"
    output:
        "analysis/tblastn/gtdb_{clade}.txt"
    params:
        db = "databases/gtdb/gtdb_genomes_reps_r214.fna",
        cols = [ 'qseqid', 'sseqid', 'pident', 'length', 'qlen', 'slen', 'qstart', 'qend', 'sstart', 'send', 'evalue', 'bitscore', 'stitle' ],
        max_seq = 1000,
        evalue = 1e-5
    conda:
        "envs/blast.yaml"
    threads:
        40
    shell:
        "tblastn -query {input.query} -db {params.db} -out {output} -outfmt '6 {params.cols}' -num_threads {threads} -max_target_seqs {params.max_seq} -evalue {params.evalue}"

rule tblastn_gtdb_entries:
    input:
        tblastn = "analysis/tblastn/gtdb_{clade}.txt",
        ndb = "databases/gtdb/gtdb_genomes_reps_r214.fna.ndb"
    output:
        "analysis/tblastn/gtdb_{clade}.fna"
    params:
        db = "databases/gtdb/gtdb_genomes_reps_r214.fna"
    conda:
        "envs/blast.yaml"
    shell:
        "cut -f2 {input.tblastn} | sort -u | blastdbcmd -entry_batch - -db {params.db} > {output}"

#rule tblastn_gtdb_extract_regions:
#    input:
#        fasta = "analysis/tblastn/gtdb_{clade}.fna",
#        tblastn = "analysis/tblastn/gtdb_{clade}.txt"
#    output:
#        "analysis/tblastn/gtdb_{clade}_cds.fna"
#    params:
#        cols = [ 'qseqid', 'sseqid', 'pident', 'length', 'qlen', 'slen', 'qstart', 'qend', 'sstart', 'send', 'evalue', 'bitscore', 'stitle' ],
#        pident_max = 90,
#        evalue_max = 1e-20,
#        split_seqid = True
#    conda:
#        "envs/biopython.yaml"
#    script:
#        "scripts/extract_regions.py"

# rule tblastn_ref_extract_regions:
#    input:
#        fasta = "analysis/ref_genomes/genomes.fna",
#        tblastn = "analysis/tblastn/ref_genomes.txt"
#    output:
#        "analysis/tblastn/ref_genomes_cds.fna"
#    params:
#        cols = [ 'qseqid', 'sseqid', 'pident', 'length', 'qlen', 'slen', 'qstart', 'qend', 'sstart', 'send', 'evalue', 'bitscore', 'stitle' ]
#    conda:
#        "envs/biopython.yaml"
#    script:
#        "scripts/extract_regions.py"

rule gtdb_heimdallr_matches:
    input:
        blastp = "analysis/blastp/GTDB-HeimdallR.txt",
        fasta = "analysis/rhodopsin_cds/GTDB/coding.faa"
    output:
        "analysis/recomb/GTDB-HeimdallR.faa"
    params:
        evalue_col = 13,
        sseqid_col = 2,
        evalue = 1e-30
    conda:
        "envs/kits.yaml"
    shell:
        "awk '${params.evalue_col}<{params.evalue}' {input.blastp} | cut -f{params.sseqid_col} | seqkit grep -f- -o {output} {input.fasta}"

rule gtdb_heimdallr_matches_cdhit:
    input:
        "analysis/recomb/GTDB-HeimdallR.faa"
    output:
        "analysis/recomb/GTDB-HeimdallR.cdhit"
    params:
        c = 0.62,
        n = 2
    conda:
        "envs/cd-hit.yaml"
    threads:
        10
    shell:
        "cd-hit -i {input} -o {output} -c {params.c} -n {params.n} -d 0 -T {threads}"

rule recomb_rhodopsin_sets:
    input:
        "rhodopsins/{rhodopsin_set}.faa"
    output:
        "analysis/recomb/{rhodopsin_set}.cdhit"
    params:
        c = 0.62,
        n = 2
    conda:
        "envs/cd-hit.yaml"
    shell:
        "cd-hit -i {input} -o {output} -c {params.c} -n {params.n} -d 0"

rule recomb_all_sequences:
    input:
        expand("analysis/recomb/{rhodopsin_set}.cdhit", rhodopsin_set = rhodopsin_sets),
        "analysis/recomb/GTDB-HeimdallR.cdhit"
    output:
        "analysis/recomb/all_sequences.faa"
    shell:
        "cat {input} > {output}"

rule recomb_cdhit:
    input:
        "analysis/recomb/all_sequences.faa"
    output:
        "analysis/recomb/all_sequences.cdhit"
    params:
        c = 0.99
    conda:
        "envs/cd-hit.yaml"
    shell:
        "cd-hit -i {input} -o {output} -c {params.c} -d 0"

rule recomb_mafft:
    input:
        "analysis/recomb/all_sequences.cdhit"
    output:
        "analysis/recomb/all_sequences.mafft"
    conda:
        "envs/phylophlan.yaml"
    threads:
        10
    shell:
        "mafft --thread {threads} --auto --reorder {input} > {output}"

rule get_trim_cds:
    input:
        "analysis/recomb/all_sequences.mafft"
    output:
        "analysis/recomb/all_sequences.mafft.trimmed.txt"
    params:
        mode = "gt 0.6"
    conda:
        "envs/phylophlan.yaml"
    shell:
        "trimal -{params.mode} -in {input} -colnumbering -out /dev/null > {output}"

rule trim_cds_aln:
    input:
        fasta = "analysis/recomb/all_sequences.mafft",
        trimal = "analysis/recomb/all_sequences.mafft.trimmed.txt"
    output:
        "analysis/recomb/all_sequences.trimmed.faa"
    conda:
        "envs/biopython.yaml"
    script:
        "scripts/trim_aln.py"

rule gard_hyphy:
    input:
        "analysis/recomb/all_sequences.trimmed.faa"
    output:
        best_gard = "analysis/recomb/gard/best-gard",
        fit_bf = "analysis/recomb/gard/best-gard.fit.bf",
        json = "analysis/recomb/gard/GARD.json"
    threads:
        20
    log:
        "analysis/recomb/gard/gard.log"
    shadow:
        "minimal"
    conda:
        "envs/hyphy.yaml"
    shell:
        """
        hyphy GARD --mode Faster --type amino-acid --rv Gamma --alignment {input} CPU={threads} &> {log}
        mv {input}.best-gard {output.best_gard}
        mv {input}.best-gard.fit.bf {output.fit_bf}
        mv {input}.GARD.json {output.json}
        """

# not under conda
rule geneconv:
    input:
        "analysis/recomb/all_sequences.trimmed.faa"
    output:
        "analysis/recomb/geneconv/geneconv.frags"
    params:
        seed = 123,
        prefix = "analysis/recomb/geneconv/geneconv"
    shadow:
        "minimal"
    log:
        "analysis/recomb/geneconv/geneconv.log"
    shell:
        """
        geneconv {input} -nolog -Seed={params.seed} -Gscale=1 -WideCols &> {log}
        mv {params.prefix}.frags {output}
        """
